# RouteProvider

An F# Type provider that generates types suitable for routing in a web application.

## Example: 

``` Fsharp
[<Literal>]
let routes = """
  GET projects/{projectId} as getProject
  GET projects/{projectId}/comments/{commentId} as getProjectComments
  PUT projects/{projectId:int} as updateProject
  GET projects/statistics
  GET people/{name:string} as getPerson
"""

type MyRoutes = IsakSky.RouteProvider<routes>

let router = MyRoutes(
              getProject = (fun projectId -> printfn "You asked for project %d" projectId),
              getProjectComments = (fun projectId commentId ->
                printfn "You asked for project %d and comment %d" projectId commentId),
              updateProject = (fun p -> printfn "Updated project %d" p),
              // If you don't provide a route name, one will be computed for you
              GET__projects_statistics = (fun () -> printfn "You asked for project statistics"),
              getPerson = (fun name -> printfn "You asked for a person called \"%s\"" name))
```

You can use ```int64```, ```int```, or ```string``` as type annotations. The default is ```int64```.

Now we can use the router like this:

    router.dispatchRoute("GET", "projects/4321/comments/1234")
    -> "You asked for project 4321 and comment 1234"

You can also build paths in a typed way like this:

    let url = MyRoutes.Builders.getProjectComments(123L,4L)
    -> "/projects/123/comments/4"
    
To integrate with the web library you are using, you can pass in a fully qualified type name as the second argument:

    type Routes = IsakSky.RouteProvider<routes, "Microsoft.Owin.IOwinContext">
    
The generated dispatch and handler functions will then take that type as the first argument. You can also specify a type name as the third argument, which will make the dispatch function have a return type, and will require all of you handlers to return that type.

## Example

Example with both input and return types specified:

![Example](/demo.png?raw=true "Example")

## Brief demo in visual studio:

https://www.youtube.com/watch?v=r7cdeTzPY58

## Roadmap / planned features
- Allow routes to be defined in a seperate file
- Add support for Guids
- Update documentation
- Add Suave Sample

## Comparison with other approaches

| Project         | Route definition mechanism                             | Bidirectional? | Type safety   |
|-----------------|:-------------------------------------------------------|:---------------|:--------------|
| ASP.NET MVC     | Reflection on attributes and method naming conventions | No             | Limited       |
| Freya           | Uri Templates                                          | Yes            | None          | 
| Suave.IO        | F# sprintf format string                               | No             | Yes           |
| bidi (Clojure)  | Data                                                   | Yes            | None          |
| Ruby on Rails   | Internal Ruby DSL                                      | Yes            | None          |
| Yesod (Haskell) | Types generated from Route DSL via Template Haskell    | Yes            | Full          |
| RouteProvider   | Types generated from Route DSL via #F Type Provider    | Yes            | Full          |

## Installation

You can install it via Nuget:

```Install-Package RouteProvider -Pre```

## How does it work?

It generates FSharp code. For example, for the routes defined above, it generates the following FSharp:

```FSharp
// Generated by RouteProvider 0.0.0.0
namespace IsakSky

open System
module Provided =
  let getProject (projectId:int64) =
      "projects/" + projectId.ToString()
  let PUT__projects (foo:string) =
      "projects/" + foo
  let createProject (projectId:int) =
      "projects/" + projectId.ToString()
  let getProjectComments (projectId:int64) (commentId:int64) =
      "projects/" + projectId.ToString() + "comments/" + commentId.ToString()

  module Internal =
    type TryParseState =
    | Untried = 0
    | Success = 1
    | Failed = 2

    let tryParseInt32 (s:string, parseState: TryParseState byref, result: int byref) =
      match parseState with
      | TryParseState.Failed
      | TryParseState.Success -> ()
      | _ ->
        parseState <- match Int32.TryParse(s, &result) with
        | true -> TryParseState.Success
        | false -> TryParseState.Failed
      parseState = TryParseState.Success

    let tryParseInt64 (s:string, parseState: TryParseState byref, result: int64 byref) =
      match parseState with
      | TryParseState.Failed
      | TryParseState.Success -> ()
      | _ ->
        parseState <- match Int64.TryParse(s, &result) with
        | true -> TryParseState.Success
        | false -> TryParseState.Failed
      parseState = TryParseState.Success

    let fakeBaseUri = new Uri("http://a.a")

    exception RouteNotMatchedException of string * string

  type MyRoutes2 =
    { getProject: int64->unit
      PUT__projects: string->unit
      createProject: int->unit
      getProjectComments: int64->int64->unit
      notFound: (string->string->unit) option }

    member inline private this.HandleNotFound(verb, path) =
      match this.notFound with
      | None -> raise (Internal.RouteNotMatchedException (verb, path))
      | Some(notFound) -> notFound verb path

    member this.DispatchRoute(verb:string, path:string) : unit =
      let parts = path.Split('/')
      let start = if parts.[0] = "" then 1 else 0
      let endOffset = if parts.Length > 0 && parts.[parts.Length - 1] = "" then 1 else 0
      match parts.Length - start - endOffset with
      | 4 ->
        if String.Equals(parts.[0 + start],"projects") then
          let mutable projectId = 0L
          let mutable projectId_parseState = Internal.TryParseState.Untried
          if Internal.tryParseInt64(parts.[1 + start], &projectId_parseState, &projectId) then
            if String.Equals(parts.[2 + start],"comments") then
              let mutable commentId = 0L
              let mutable commentId_parseState = Internal.TryParseState.Untried
              if Internal.tryParseInt64(parts.[3 + start], &commentId_parseState, &commentId) then
                if verb = "GET" then this.getProjectComments projectId commentId
                else this.HandleNotFound(verb, path)
              else this.HandleNotFound(verb, path)
            else this.HandleNotFound(verb, path)
          else this.HandleNotFound(verb, path)
        else this.HandleNotFound(verb, path)
      | 2 ->
        if String.Equals(parts.[0 + start],"projects") then
          let mutable int64ArgDepth_1 = 0L
          let mutable int64ArgDepth_1_parseState = Internal.TryParseState.Untried
          let mutable intArgDepth_1 = 0
          let mutable intArgDepth_1_parseState = Internal.TryParseState.Untried
          if Internal.tryParseInt64(parts.[1 + start], &int64ArgDepth_1_parseState, &int64ArgDepth_1) then
            if verb = "GET" then this.getProject int64ArgDepth_1
            elif verb = "PUT" then this.PUT__projects (parts.[1 + start])
            else this.HandleNotFound(verb, path)
          elif Internal.tryParseInt32(parts.[1 + start], &intArgDepth_1_parseState, &intArgDepth_1) then
            if verb = "POST" then this.createProject intArgDepth_1
            elif verb = "PUT" then this.PUT__projects (parts.[1 + start])
            else this.HandleNotFound(verb, path)
          else
            if verb = "PUT" then this.PUT__projects (parts.[1 + start])
            else this.HandleNotFound(verb, path)
        else this.HandleNotFound(verb, path)
      | _ ->
        this.HandleNotFound(verb, path)

    member this.DispatchRoute(verb:string, uri:Uri) : unit =
      // Ensure we have an Absolute Uri, or just about every method on Uri chokes
      let uri = if uri.IsAbsoluteUri then uri else new Uri(Internal.fakeBaseUri, uri)
      let path = uri.GetComponents(UriComponents.Path, UriFormat.Unescaped)
      this.DispatchRoute(verb, path)
```

